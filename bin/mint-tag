#!/usr/bin/env perl
# PODNAME: mint-tag
# ABSTRACT: build git tags from labeled pull/merge requests

use v5.20;
use lib 'lib';
use App::MintTag;

binmode *STDOUT, ':encoding(UTF-8)';
binmode *STDERR, ':encoding(UTF-8)';

use App::MintTag::Logger '$Logger' => { init => {
  ident     => 'mint-tag',
  facility  => 'daemon',
  to_stdout => 1,
  log_pid   => 0,
} };

use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'config|c=s', 'config file to use', { required => 1 } ],
  [ 'sha=s',      'do not do any merging, just tag this sha' ],
  [ 'auto',       'do not run in interactive mode' ],
  [ 'verbose|v',  'turn on debug logging' ],
  [ 'help|h',     'show help and exit', { shortcircuit => 1 } ],
);

print($usage->text), exit if $opt->help;

if ($opt->sha && $opt->auto) {
  $usage->die({ pre_text => "error: --sha must be run in manual mode\n"});
}

local $Logger = $Logger;
unless ($opt->auto) {
  # Nicer logs if someone is looking at it.
  $Logger = $Logger->proxy({ proxy_prefix => 'mint-tag | '});
}

$Logger->set_debug(1) if $opt->verbose;

my $minttag = App::MintTag->from_config_file($opt->config);

if ($opt->sha) {
  $minttag->tag_arbitrary_sha($opt->sha);
  exit 0;
}

$minttag->mint_tag($opt->auto);
